"""Repository rule that queries pkg-config and exports flags as a .bzl file."""

def _parse_flags(flags_str, prefix):
    """Extract values with given prefix (e.g. '-I', '-l', '-L') from a flags string."""
    return [f[len(prefix):] for f in flags_str.split(" ") if f.startswith(prefix)]

def _pkg_config_impl(rctx):
    pkg = rctx.attr.pkg
    result = rctx.execute(["pkg-config", "--cflags", pkg])
    if result.return_code != 0:
        fail("pkg-config --cflags {} failed: {}".format(pkg, result.stderr))
    cflags = result.stdout.strip()

    result = rctx.execute(["pkg-config", "--libs", pkg])
    if result.return_code != 0:
        fail("pkg-config --libs {} failed: {}".format(pkg, result.stderr))
    libs = result.stdout.strip()

    includes = _parse_flags(cflags, "-I")
    defines = _parse_flags(cflags, "-D")
    linkopts_l = ["-l" + l for l in _parse_flags(libs, "-l")]
    linkopts_L = ["-L" + l for l in _parse_flags(libs, "-L")]

    # pkg-config omits -L for standard paths, but hermetic toolchains need them.
    if not linkopts_L:
        result = rctx.execute(["pkg-config", "--variable=libdir", pkg])
        if result.return_code == 0 and result.stdout.strip():
            linkopts_L = ["-L" + result.stdout.strip()]
    other_copts = [f for f in cflags.split(" ") if f and not f.startswith("-I") and not f.startswith("-D")]

    # Write a .bzl file exporting the flags as constants.
    # INCLUDES are absolute system include paths (propagates to dependents via -isystem).
    # COPTS are other flags like -pthread, -msse.
    bzl_content = """\
# Auto-generated by pkg_config repository rule for "{pkg}".
# Do not edit.
INCLUDES = {includes}
COPTS = {copts}
DEFINES = {defines}
LINKOPTS = {linkopts}
""".format(
        pkg = pkg,
        includes = repr(includes),
        copts = repr(other_copts),
        defines = repr(defines),
        linkopts = repr(linkopts_L + linkopts_l),
    )

    build_content = """\
# Auto-generated by pkg_config repository rule for "{pkg}".
# Do not edit.
load("@rules_system_deps//private:system_library.bzl", "system_library")

system_library(
    name = "{name}",
    includes = {includes},
    defines = {defines},
    linkopts = {linkopts},
    visibility = ["//visibility:public"],
)
""".format(
        pkg = pkg,
        name = rctx.attr.target_name,
        includes = repr(includes),
        defines = repr(defines),
        linkopts = repr(linkopts_L + linkopts_l),
    )

    rctx.file("flags.bzl", bzl_content)
    rctx.file("BUILD.bazel", build_content)

pkg_config = repository_rule(
    implementation = _pkg_config_impl,
    attrs = {
        "pkg": attr.string(mandatory = True, doc = "pkg-config package name"),
        "target_name": attr.string(mandatory = True, doc = "Target name in generated BUILD file"),
    },
    local = True,
    doc = "Queries pkg-config and exports flags as a .bzl file.",
)
